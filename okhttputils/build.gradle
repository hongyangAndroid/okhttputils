plugins {
    id 'com.android.library'
}

apply from: "$rootProject.projectDir/publishing.gradle"

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33
        versionCode 1
        versionName "2.6.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions{
        abortOnError false
        warning 'InvalidPackage'
    }

    publishing {
        singleVariant('release')
    }
}

task clearJar(type: Delete) {
    delete 'build/outputs/okhttputils.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('build/outputs/')
    include('classes.jar')
    rename ('classes.jar', 'okhttputils-2_6_2.jar')
}

makeJar.dependsOn(clearJar, build)

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.zhy'
                artifactId = 'okhttputils'
                version = "$project.android.defaultConfig.versionName"

                pom {
                    name = 'okhttputils'
                    description = 'Oh hi, this is a nice description for a project, right?'
                    url = 'https://github.com/hongyangAndroid/okhttputils'
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'hongyangandroid'
                            name = 'Hongyang Android'
                        }
                    }
                    scm {
                        connection = 'scm:hg:https://github.com/hongyangAndroid/okhttputils'
                        developerConnection = 'scm:hg:https://github.com/hongyangAndroid/okhttputils'
                        url = 'https://github.com/hongyangAndroid/okhttputils'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}
